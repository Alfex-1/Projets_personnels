/* Importation de la base */
%let filepath = /home/u62439365/Perso ;
%let data_name = Ventes.csv ;
%let datafile = "&filepath./&data_name.";

proc import datafile=&datafile. out=data dbms=csv replace;
	delimiter=';';
run;

/* Traitement de la base : regroupement par la somme */
proc sql;
	create table data2 as select Date, Avg(Total_Amount) as Total_Amount
	from data group by Date
	order by Date;
quit;

/* Imputation des valeurs manquantes par la première valeur de la période précédente */
proc timedata data=Data2 seasonality=7 out=Data3;
	id Date interval=day setmissing=previous;
	var Total_Amount / accumulate=total transform=none;
run;

/******************** Exploration de la série temporelle ********************/

ods noproctitle;
ods graphics / imagemap=on;
ods rtf file="/home/u62439365/Perso/Graphiques.rtf" 
	style=Journal1a startpage=no;

/* Analyse descriptive et spectrale */
proc timeseries data=Work.Data3 seasonality=3 plots=(series cycles 
		spectrum corr decomp) print=(descstats seasons decomp);
	id Date interval=day;
	var Total_Amount / accumulate=none transform=none dif=0 sdif=0;
	decomp / mode=multoradd;
	spectra / domain=frequency;
run;

/* Analyse du test de racine unitaire */
proc arima data=Work.Data3 plots=none;
	ods select StationarityTests;
	identify var=Total_Amount stationarity=(adf=7);
	run;
quit;
/* Pas besoin de différencier la série, elle est déjà stationnaire */

/* Estimation et prévisions */
proc arima data=Work.Data3 plots=all out=work.out;
	identify var=Total_Amount outcov=work.outcov;
	estimate p=(1) q=(1) method=ML outest=work.outest outstat=work.outstat 
		outmodel=work.outmodel;
	forecast lead=30 back=0 alpha=0.05 id=Date interval=day;
	outlier;
	run;
quit;
ods rtf close;